""" This extension declares asterius related external repositories.
The `custom` tag can be used to declare a pre-existing webpack instalation instead of rules_haskell default one.
"""

load(
    ":asterius_dependencies.bzl",
    "asterius_dependencies_custom",
)

_custom_tag = tag_class(
    attrs = {
        "webpack_cli_package_json_bzl": attr.label(
            doc = "The label of a webpack-cli/package_json.bzl file generated by rules_js. For instance via `npm_translate_lock`.",
        ),
    },
    doc = "Use webpack from a custom rules_js setup instead of rules_haskell default one",
)

def _select_webpack(mctx):
    # Select the first custom tag according to the default iteration order over modules where the root module comes first.
    for module in mctx.modules:
        for _index, custom_tag in enumerate(module.tags.custom):
            if custom_tag.webpack_cli_package_json_bzl:
                return custom_tag.webpack_cli_package_json_bzl
    return "@rules_haskell_npm//haskell/asterius/npm:webpack-cli/package_json.bzl"

def _rules_haskell_asterius_impl(mctx):
    asterius_dependencies_custom(webpack_cli_package_json_bzl = _select_webpack(mctx))

rules_haskell_asterius = module_extension(
    implementation = _rules_haskell_asterius_impl,
    tag_classes = {
        "custom": _custom_tag,
    },
)
