workspace(name = "rules_haskell_examples")

local_repository(
    name = "rules_haskell",
    path = "..",
)

local_repository(
    name = "arm",
    path = "arm",
)

load("@rules_haskell//haskell:repositories.bzl", "rules_haskell_dependencies")

rules_haskell_dependencies()

load("@rules_haskell//haskell:nixpkgs.bzl", "haskell_register_ghc_nixpkgs")

GHC_VERSION = "9.4.8"

haskell_register_ghc_nixpkgs(
    attribute_path = "haskell.compiler.ghc{}".format(GHC_VERSION.replace(".", "")),
    repository = "@rules_haskell//nixpkgs:default.nix",
    version = GHC_VERSION,
)

load("@rules_haskell//haskell:toolchain.bzl", "rules_haskell_toolchains")

rules_haskell_toolchains(
    dist = {
        "linux_amd64": "deb10",
    },
    version = GHC_VERSION,
)

load("@rules_nixpkgs_cc//:cc.bzl", "nixpkgs_cc_configure")
load("@rules_nixpkgs_python//:python.bzl", "nixpkgs_python_configure")

nixpkgs_cc_configure(
    # Don't override the default cc toolchain needed for bindist mode.
    name = "nixpkgs_config_cc",
    cc_std = "c++14",
    repository = "@rules_haskell//nixpkgs:default.nix",
)

nixpkgs_python_configure(
    repository = "@rules_haskell//nixpkgs:default.nix",
)

load(":non_module_deps.bzl", "repositories")

repositories(bzlmod = False)

load("@rules_haskell//haskell:cabal.bzl", "stack_snapshot")

stack_snapshot(
    name = "stackage",
    components = {
        "attoparsec": [
            "lib",
            "lib:attoparsec-internal",
        ],
    },
    components_dependencies = {
        "attoparsec": """{"lib:attoparsec": ["lib:attoparsec-internal"]}""",
    },
    extra_deps = {
        "zlib": ["@zlib.hs"],
        "streaming-commons": ["@zlib.hs"],
    },
    flags = {
        # Sets the default explicitly to demonstrate the flags attribute.
        "zlib": [
            "-non-blocking-ffi",
            "-pkg-config",
        ],
    },
    local_snapshot = "@rules_haskell//:stackage_snapshot.yaml",
    packages = [
        "base",
        "bytestring",
        "conduit",
        "conduit-extra",
        "hspec",
        "optparse-applicative",
        "text",
        "text-show",
    ],
    # TODO(cb) remove once we use Cabal >= 3.10.1 / GHC >= 9.6.1
    # See https://github.com/tweag/rules_haskell/issues/1871
    setup_deps = {
        pkg: ["@Cabal//:Cabal"]
        for pkg in [
            "bifunctors",
            "call-stack",
            "conduit",
            "conduit-extra",
            "generic-deriving",
            "hspec",
            "hspec-core",
            "hspec-discover",
            "hspec-expectations",
            "HUnit",
            "mono-traversable",
            "optparse-applicative",
            "quickcheck-io",
            "split",
            "text-show",
            "transformers-compat",
            "typed-process",
            "unliftio-core",
        ]
    },
    # This example uses an unpinned version of stack_snapshot, meaning that stack is invoked on every build.
    # To switch to pinned stackage dependencies, run `bazel run @stackage-unpinned//:pin` and
    # uncomment the following line.
    # And uncomment the following line.
    vendored_packages = {"split": "@split//:split"},
)
